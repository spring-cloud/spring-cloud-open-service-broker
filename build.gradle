/*
 * Copyright 2002-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("io.spring.nohttp:nohttp-gradle:0.0.5.RELEASE")
		classpath("io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE")
	}
}

ext {
	springBootVersion = project.findProperty("springBootVersion") ?: "2.3.1.RELEASE"
	springFrameworkVersion = project.findProperty("springFrameworkVersion") ?: "5.2.7.RELEASE"
	reactorVersion = project.findProperty("reactorVersion") ?: "Dysprosium-SR8"
	springCloudContractVersion = "2.2.3.RELEASE"
	blockHoundVersion = "1.0.3.RELEASE"
	junitPlatformLauncherVersion = "1.5.2"
	checkstyleVersion = "8.33"
	pmdVersion = "6.24.0"

	javadocLinks = [
			"https://docs.oracle.com/javase/8/docs/api/",
			"https://docs.spring.io/spring/docs/${springFrameworkVersion}/javadoc-api/",
	] as String[]
}

//override managed Spring Boot versions
if (project.hasProperty("springFrameworkVersion")) {
	ext['spring-framework.version'] = ext.springFrameworkVersion
}
if (project.hasProperty("reactorVersion")) {
	ext['reactor-bom.version'] = ext.reactorVersion
}

description = "Spring Cloud Open Service Broker"

apply plugin: "io.spring.nohttp"

nohttp {
	whitelistFile = rootProject.file("src/nohttp/whitelist.lines")
}

configure(allprojects) {
	group = "org.springframework.cloud"

	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "idea"
	apply plugin: "io.spring.dependency-management"

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/release" }
	}
	buildscript {
		repositories {
			mavenCentral()
			maven { url "https://repo.spring.io/plugins-release" }
		}
	}
	if (!version.endsWith('RELEASE')) {
		repositories {
			maven { url "https://repo.spring.io/milestone" }
		}
		buildscript {
			repositories {
				maven { url "https://repo.spring.io/milestone" }
			}
		}
	}
	if (version.endsWith('SNAPSHOT') || project.hasProperty("springFrameworkVersion") ||
			project.hasProperty("springBootVersion")) {
		repositories {
			maven { url "https://repo.spring.io/snapshot" }
		}
		buildscript {
			repositories {
				maven { url "https://repo.spring.io/snapshot" }
			}
		}
	}
}

configure(allprojects - [project(":spring-cloud-open-service-broker-acceptance-webflux"),
						 project(":spring-cloud-open-service-broker-acceptance-webmvc"),
						 project(":spring-cloud-open-service-broker-contract-tests"),
						 project(":spring-cloud-open-service-broker-docs")]) {
	apply plugin: "java-library"
	apply from: "${rootProject.projectDir}/publish-maven.gradle"
	apply plugin: "jacoco"
}

configure(allprojects - [project(":spring-cloud-open-service-broker-docs")]) {
	apply plugin: "checkstyle"
	apply plugin: "pmd"

	checkstyle {
		configFile = rootProject.file("src/checkstyle/checkstyle.xml")
		toolVersion = "${checkstyleVersion}"
		showViolations = true
	}
	checkstyleMain {
		source = "src/main/java"
	}
	checkstyleTest {
		source = "src/test/java"
	}

	pmd {
		toolVersion = "${pmdVersion}"
		consoleOutput = true
	}
	pmdMain {
		ruleSets = []
		ruleSetFiles = rootProject.files("src/pmd/pmdRuleSet.xml")
		source = "src/main/java"
	}
	pmdTest {
		ruleSets = []
		ruleSetFiles = rootProject.files("src/pmd/pmdTestRuleSet.xml")
		source = "src/test/java"
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
		}
		reports.junitXml.enabled = true
	}
}

subprojects {
	task dependencyReport(type: DependencyReportTask)
}

configure(subprojects - [project(":spring-cloud-starter-open-service-broker")]) {
	[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:-processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:fallthrough",
			"-Xlint:rawtypes",
			// Suppress deprecation warnings when building against latest integration releases
			(springBootVersion == "latest.integration" || springFrameworkVersion == "latest.integration")
					? "-Xlint:-deprecation" : "-Xlint:deprecation",
			"-Xlint:unchecked",
			"-Xlint:options",
			"-Werror"
	]

	javadoc {
		description = "Generates project-level javadoc for use in -javadoc jar"

		options.memberLevel = JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = project.name
		options.use = true
		options.links(javadocLinks)
		options.addStringOption('Xdoclint:none', '-quiet')
	}

	// see https://docs.gradle.org/current/userguide/java_plugin.html
	java {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		withSourcesJar()
		withJavadocJar()

		// define 'optional' dependency feature variant
		// see https://docs.gradle.org/current/userguide/feature_variants.html
		registerFeature('optional') {
			usingSourceSet(sourceSets.main)
		}
	}

	task testsJar(type: Jar) {
		archiveClassifier.set("tests")
		from sourceSets.test.output
	}

	artifacts {
		archives testsJar
	}
}

configure(rootProject) {
	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier.set("sources")
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		archiveClassifier.set("javadoc")
		from javadoc.destinationDir
	}

	// filter unwanted subprojects from published javadoc
	def sourceProjects = subprojects - [project(":spring-cloud-open-service-broker-acceptance-webflux"),
										project(":spring-cloud-open-service-broker-acceptance-webmvc"),
										project(":spring-cloud-open-service-broker-contract-tests"),
										project(":spring-cloud-open-service-broker-docs")]

	task apidocs(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"

		options.memberLevel = JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		options.links(javadocLinks)

		source sourceProjects.collect { project ->
			project.sourceSets.main.allJava
		}

		classpath = files(sourceProjects.collect { project ->
			project.sourceSets.main.compileClasspath
		})

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "apidocs")
	}

	task docsZip(type: Zip, dependsOn: [':spring-cloud-open-service-broker-docs:asciidoctor']) {
		group = "Distribution"
		archiveClassifier.set("docs")
		description = "Builds -${archiveClassifier} archive containing api and reference " +
				"for deployment."

		from(apidocs) {
			into "apidocs"
		}
		println project.tasks.findByPath(':spring-cloud-open-service-broker-docs:asciidoctor')
		from(project.tasks.findByPath(':spring-cloud-open-service-broker-docs:asciidoctor')) {
			into 'reference'
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact sourcesJar
				artifact javadocJar
				artifact docsZip
			}
		}
	}

	task dist(dependsOn: assemble) {
		group = "Distribution"
		description = "Builds -dist and -docs distribution archives."
	}
}

task codeCoverageReport(type: JacocoReport) {
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

	subprojects.each {
		sourceSets it.sourceSets.main
	}

	reports {
		xml.enabled true
		xml.destination new File("${buildDir}/reports/jacoco/report.xml")
		html.enabled false
		csv.enabled false
	}
}

codeCoverageReport.dependsOn {
	subprojects*.test
}

wrapper {
	gradleVersion = "6.5"
	distributionType = Wrapper.DistributionType.ALL
}
