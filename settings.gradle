/*
 * Copyright 2002-2022 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
	plugins {
		id "org.springframework.boot" version "${springBootVersion}"
		id "io.spring.nohttp" version "0.0.10"
		id 'org.asciidoctor.jvm.pdf' version '3.3.2'
		id 'org.asciidoctor.jvm.convert' version '3.3.2'
	}
	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven { url "https://repo.spring.io/plugins-release" }
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/plugins-snapshot" }
		}
	}
}

dependencyResolutionManagement {
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/release' }
	}
	if (version =~ /((-M|-RC)[0-9]+|-SNAPSHOT)$/) {
		repositories {
			maven { url 'https://repo.spring.io/milestone' }
		}
	}
	if (version.endsWith('-SNAPSHOT')) {
		repositories {
			maven { url 'https://repo.spring.io/snapshot' }
		}
	}
}

rootProject.name = "spring-cloud-open-service-broker"

include ":spring-cloud-open-service-broker-docs"
include ":spring-cloud-open-service-broker-core"
include ":spring-cloud-open-service-broker-autoconfigure"
include ":spring-cloud-starter-open-service-broker"
include ":spring-cloud-open-service-broker-contract-tests"
include ":spring-cloud-open-service-broker-acceptance-webflux"
include ":spring-cloud-open-service-broker-acceptance-webmvc"
